subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    group = 'com.elevenware.quickpki'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                pom {
                    name = 'quick-pki'
                    description = 'A simple, self-contained PKI for development and testing'
                    url = 'https://github.com/georgecodes/quick-pki'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    groupId = "com.elevenware.quickpki"
                    version = version
                    developers {
                        developer {
                            id = 'georgecodes'
                            name = 'George McIntosh'
                            email = 'george@elevenware.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/georgecodes/quick-pki.git'
                        developerConnection = 'scm:git:https://github.com/georgecodes/quick-pki.git'
                        url = 'https://github.com/georgecodes/quick-pki'
                    }
                }
            }
        }
        repositories {
            maven {

                def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
                def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                name = 'nexus'
                credentials(PasswordCredentials)
            }
        }

    }

    jar {
        manifest.attributes provider: 'gradle'
    }

    signing {
        useGpgCmd()
//        sign configurations.runtimeElements
        sign publishing.publications.maven
    }

}
